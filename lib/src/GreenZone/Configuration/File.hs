{-# LANGUAGE LambdaCase        #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE Trustworthy       #-}

module GreenZone.Configuration.File where

import           Data.Monoid                        ( (<>) )

import           Control.Exception.Safe             ( Exception, throw )
import           Control.Monad.IO.Class             ( MonadIO, liftIO )
import qualified Data.ByteString                    as BS
import           Data.Text                          ( Text, intercalate )
import qualified Data.Yaml                          as Yaml

import           GreenZone.Configuration.Types      ( Configuration (..) )
import           GreenZone.Configuration.Validators ( ErrorReport (..),
                                                      validate )
import           GreenZone.Paths                    ( pathToConfigurationFile )
import           GreenZone.Tool.Text                ( showT )

-- | Possible problems with configuration file.
newtype ConfigurationProblem = ConfigurationProblem Text
    deriving Show

instance Exception ConfigurationProblem

-- | Reads configuration from the configuration file.
readConfiguration
    :: MonadIO m
    => FilePath
    -> m Configuration
readConfiguration pathToConfig =
    liftIO $ Yaml.decodeFileEither pathToConfig >>= \case
        Left problem -> throw . ConfigurationProblem $ showT problem
        Right config -> case validate config of
            Nothing -> return config
            Just (ErrorReport errors) -> throw . ConfigurationProblem $ prepare errors
  where
    prepare errors = "errors found: " <> intercalate "; " errors

-- | Stores configuration in the configuration file.
storeConfiguration
    :: MonadIO m
    => Configuration
    -> m ()
storeConfiguration config =
    liftIO $ BS.writeFile pathToConfigurationFile completeConfig
  where
    completeConfig = warning <> Yaml.encode config
    warning =    "# THIS FILE WAS GENERATED BY GREENZONE MANAGER.\n"
              <> "# IT IS STRONGLY RECOMMENDED TO USE GREENZONE MANAGER FOR RECONFIGURATION.\n"
              <> "# PLEASE DO NOT CHANGE THESE VALUES MANUALLY UNLESS YOU REALLY UNDERSTAND WHAT YOU ARE DOING.\n"
